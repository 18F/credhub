buildscript {
    ext {
        springBootVersion = '1.3.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.owasp:dependency-check-gradle:1.3.6")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'jacoco'

jar {
    baseName = 'sec-eng-credential-manager'
    version = '0.1.0'
}

ext {
    buildVersion = "${jar.version} build ${System.getenv("BUILD_NUMBER") ?: "DEV"}"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.security.oauth:spring-security-oauth2')
    compile('org.springframework.security:spring-security-jwt')
    compile('org.passay:passay:1.1.0')
    compile('com.h2database:h2')
    compile('com.jayway.jsonpath:json-path:2.2.0')
    compile('org.bouncycastle:bcpkix-jdk15on:1.52')
    compile('com.google.guava:guava:19.0')
    compile('org.apache.commons:commons-lang3:3.4')
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.5')
    compile('org.exparity:hamcrest-bean:1.0.11')
    compile('org.postgresql:postgresql:9.3-1100-jdbc4')
    compile('mysql:mysql-connector-java:5.1.38')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.skyscreamer:jsonassert')
    testCompile('com.greghaskins:spectrum:0.6.1')
    testCompile('org.apache.commons:commons-lang3:3.4')
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

processResources {
    expand(project.properties)
    outputs.upToDateWhen { false }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/reports/jacoco"
    }
}

bootRun {
    addResources = true
    systemProperties["spring.profiles.active"] = System.getProperty("spring.profiles.active", "dev")
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
    systemProperties["spring.profiles.active"] = "dev"
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

tasks.check.dependsOn tasks.jacocoTestReport
tasks.check.dependsOn tasks.dependencyCheck
